#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = false
ScannerTable = Full
Input = Stream
Parser = LL
#RegularDefinitions
letra: [a-z A-Z]
digito: [0-9]
#Tokens
// identificadores
id: {letra} ((_)?({letra}|{digito})+)*(_)?

numero: {digito}+
literal: \'[^\'\n\r]*\'

//Palavrasreservadas
CREATE = id:"CREATE"
DATABASE = id:"DATABASE"
TABLE = id:"TABLE"
INDEX = id:"INDEX"
ON = id:"ON"
INSERT = id:"INSERT"
INTO = id:"INTO"
VALUES = id:"VALUES"
SELECT = id:"SELECT"
FROM = id:"FROM"
WHERE = id:"WHERE"
AND = id:"AND"
OR = id:"OR"
DROP = id:"DROP"
DESCRIBE = id:"DESCRIBE"
SET = id:"SET"
ORDER = id:"ORDER"
BY = id:"BY"
DESC = id:"DESC"
AS = id:"AS"
PRIMERY = id:"PRIMERY"
FOREIGN = id:"FOREIGN"
KEY = id:"KEY"
REFERENCES = id:"REFERENCES"
JOIN = id:"JOIN"
INNER = id:"INNER"
LEFT = id:"LEFT"
RIGHT = id:"RIGHT"

integer = id:"integer"
varchar = id:"varchar"
char = id:"char"
null = id:"null"

// Simbolos

"=" 
">" 
"<" 
">=" 
"<=" 
"<>"

"."
","
"*"
";"
"`"
"("
")"

// Ignorados

:![\s\n\t]
// comentario de linha
:! "-""-"[^\n]*
:! "/*"[^/\*]*"*/"
#NonTerminals
<COMANDO>
<ACAO>
<CRIAR>              
<CRIAR2>  
<PRIMERY>   
<FOREIGN>        
<TEMFOREIGN>
<ATRIBUTOS>         
<ATRIBUTO>          
<RATRIBUTOS>        
<TIPO>              
<LISTAIDS>          
<RLISTAIDS>         
<INCLUIR>           
<COLUNAS>           
<LISTAVALORES>      
<VALOR>             
<RLISTAVALORES>     
<SELECIONAR>        
<CAMPOS>            
<RCAMPOS>
<CAMPOS2>          
<CAMPO>             
<AS>
<CAMPO2>
<TABELAS>           
<RTABELAS>          
<TABELA>            
<CLAUSULA_WHERE>
<CLAUSUALA_ORDER> 
<DESC>
<CLAUSULA_JOIN>   
<CONDICOES>         
<CONDICAO>          
<CONDICAO2>
<RCONDICAO>
<OPERADOR_REL>
<OPERADOR_LOG>
<EXCLUIR>
<EXCLUIR2>  
<DESCREVER>
<SETAR_BANCO>
#Grammar
<COMANDO>          ::= <ACAO> ";";

<ACAO>              ::= <CRIAR> | <INCLUIR> | <SELECIONAR> | <EXCLUIR> | <DESCREVER> | <SETAR_BANCO> ;

<CRIAR>              ::= CREATE <CRIAR2> ;

<CRIAR2>             ::= DATABASE id | TABLE id "(" <ATRIBUTOS> <PRIMERY><TEMFOREIGN>")" | INDEX id  ON id "(" <LISTAIDS> ")";
<PRIMERY>	    ::= PRIMERY KEY "("<LISTAIDS>")" | î;
<TEMFOREIGN>	    ::= <FOREIGN> | î;
<FOREIGN>	    ::= FOREIGN KEY "("id")" REFERENCES id "("id")" <TEMFOREIGN>| î;

<ATRIBUTOS>         ::= <ATRIBUTO> <RATRIBUTOS>;

<ATRIBUTO>          ::= id <TIPO>  ;

<RATRIBUTOS>        ::= "," <ATRIBUTOS> | î;

<TIPO>              ::= integer | varchar "(" numero ")" | char "(" numero ")";

<LISTAIDS>          ::= id <RLISTAIDS>;

<RLISTAIDS>         ::= "," id <LISTAIDS> | î;

<INCLUIR>           ::= INSERT INTO id  <COLUNAS> VALUES "(" <LISTAVALORES> ")" ;

<COLUNAS>           ::= "(" <LISTAIDS> ")" | î;

<LISTAVALORES>      ::= <VALOR> <RLISTAVALORES>;

<VALOR>             ::= numero | literal | null;

<RLISTAVALORES>     ::= "," <LISTAVALORES> | î;

<SELECIONAR>        ::= SELECT <CAMPOS> FROM <TABELAS> <CLAUSULA_JOIN> <CLAUSULA_WHERE> <CLAUSUALA_ORDER>;

<CAMPOS>            ::= id "." <CAMPOS2>;

<CAMPOS2>	   ::= id <AS> <RCAMPOS> | "*";

<RCAMPOS>           ::= "," <CAMPO> <RCAMPOS> | î;

<CAMPO>             ::= id"."id <AS>;
<AS>		   ::= AS "`" id "`" | î;

<TABELAS>           ::= <TABELA> <RTABELAS>;

<RTABELAS>          ::= "," <TABELAS> | î;

<TABELA>            ::= id ;

<CLAUSULA_WHERE>    ::= WHERE <CONDICOES>| î;
<CLAUSUALA_ORDER>   ::= ORDER BY <CAMPO><RCAMPOS> <DESC>| î;
<DESC>		    ::= DESC | î;
<CLAUSULA_JOIN>     ::= î | INNER JOIN id | LEFT JOIN id | RIGHT JOIN id;

<CONDICOES>         ::= <CONDICAO> <RCONDICAO>;

<CONDICAO>          ::= <CAMPO2> <OPERADOR_REL> <CONDICAO2>;
<CONDICAO2>	   ::= <CAMPO2> | <VALOR>;
<CAMPO2>             ::= id"."id;

<RCONDICAO>         ::= <OPERADOR_LOG> <CONDICOES> |  î;

<OPERADOR_REL>      ::= "=" | ">" | "<" | ">=" | "<=" | "<>";

<OPERADOR_LOG>      ::= AND | OR;

<EXCLUIR>	    ::= DROP <EXCLUIR2>;

<EXCLUIR2>          ::= TABLE id | INDEX id; 

<DESCREVER>         ::= DESCRIBE id; 

<SETAR_BANCO>       ::= SET DATABASE id;
