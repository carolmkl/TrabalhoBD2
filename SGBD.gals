#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = false
ScannerTable = Full
Input = String
Parser = LL
#RegularDefinitions
letra: [a-z A-Z]
digito: [0-9]
#Tokens
// identificadores
id: {letra} ((_)?({letra}|{digito})+)*(_)?

numero: {digito}+
literal: "'"[^"'"\n\r]*"'"

//Palavrasreservadas
CREATE = id:"CREATE"
DATABASE = id:"DATABASE"
TABLE = id:"TABLE"
INDEX = id:"INDEX"
ON = id:"ON"
INSERT = id:"INSERT"
INTO = id:"INTO"
VALUES = id:"VALUES"
SELECT = id:"SELECT"
FROM = id:"FROM"
WHERE = id:"WHERE"
AND = id:"AND"
OR = id:"OR"
DROP = id:"DROP"
DESCRIBE = id:"DESCRIBE"
SET = id:"SET"
ORDER = id:"ORDER"
BY = id:"BY"
DESC = id:"DESC"
AS = id:"AS"
PRIMARY = id:"PRIMARY"
FOREIGN = id:"FOREIGN"
KEY = id:"KEY"
REFERENCES = id:"REFERENCES"
JOIN = id:"JOIN"
INNER = id:"INNER"
LEFT = id:"LEFT"
RIGHT = id:"RIGHT"

integer = id:"integer"
varchar = id:"varchar"
char = id:"char"
null = id:"null"

// Simbolos

"=" 
">" 
"<" 
">=" 
"<=" 
"<>"

"."
","
"*"
";"
"`"
"("
")"

// Ignorados

:![\s\n\t\r]
// comentario de linha
:! "-""-"[^\n\r]*
:! "/*"[^/\*]*"*/"
#NonTerminals
<COMANDO>
<RCOMANDO>
<ACAO>
<CRIAR>              
<CRIAR2>  
<PRIMERY>   
<FOREIGN>        
<TEMFOREIGN>
<ATRIBUTOS>         
<ATRIBUTO>          
<RATRIBUTOS>        
<TIPO>              
<LISTAIDS>          
<RLISTAIDS>         
<INCLUIR>           
<COLUNAS>           
<LISTAVALORES>      
<VALOR>             
<RLISTAVALORES>     
<SELECIONAR>        
<CAMPOS>            
<RCAMPOS>
<CAMPOS2>          
<CAMPO>             
<AS>
<CAMPO2>
<TABELAS>           
<RTABELAS>          
<TABELA>            
<CLAUSULA_WHERE>
<CLAUSUALA_ORDER> 
<DESC>
<CLAUSULA_JOIN>   
<CONDICOES>         
<CONDICAO>          
<CONDICAO2>
<RCONDICAO>
<OPERADOR_REL>
<OPERADOR_LOG>
<EXCLUIR>
<EXCLUIR2>  
<DESCREVER>
<SETAR_BANCO>
#Grammar
// a—É—àa 0 executa e limpa as estruturas
<COMANDO>          ::= <ACAO> #0";" <RCOMANDO>;
<RCOMANDO>	  ::= <COMANDO> | Ó;

<ACAO>              ::= <CRIAR> | <INCLUIR> | <SELECIONAR> | <EXCLUIR> | <DESCREVER> | <SETAR_BANCO> ;

<CRIAR>              ::= CREATE <CRIAR2> ;

<CRIAR2>             ::= DATABASE id#1 | TABLE id#2 "(" <ATRIBUTOS>#6 <PRIMERY><TEMFOREIGN>")" | INDEX id#3  ON id#4 "(" <LISTAIDS> ")";
// esvaziar a lista de ids
<PRIMERY>	    ::= PRIMARY KEY "("<LISTAIDS>")" #6| Ó;
<TEMFOREIGN>	    ::= Ó | <FOREIGN>;
<FOREIGN>	    ::= FOREIGN KEY "("id#7")" REFERENCES id#8 "("id#9")" <TEMFOREIGN>;

<ATRIBUTOS>         ::= <ATRIBUTO> <RATRIBUTOS>;

<ATRIBUTO>          ::= id#5 <TIPO>  ;

<RATRIBUTOS>        ::= "," <ATRIBUTOS> | Ó;

// tirar quem foi o ultimo armazenado
<TIPO>              ::= integer#10 | varchar "(" numero#11 ")" | char "(" numero#12 ")";

<LISTAIDS>          ::= id#5 <RLISTAIDS>;

<RLISTAIDS>         ::= "," id#5 <RLISTAIDS> | Ó;

<INCLUIR>           ::= INSERT INTO id#5  <COLUNAS>#19 VALUES "(" <LISTAVALORES> ")";

<COLUNAS>           ::= "(" <LISTAIDS> ")" | Ó;

<LISTAVALORES>      ::= <VALOR>#20 <RLISTAVALORES>;

<VALOR>             ::= numero | literal | null;

<RLISTAVALORES>     ::= "," <LISTAVALORES> | Ó;

<SELECIONAR>        ::= SELECT <CAMPOS> FROM <TABELAS> <CLAUSULA_JOIN> <CLAUSULA_WHERE> <CLAUSUALA_ORDER>;

<CAMPOS>            ::= id#5 "." <CAMPOS2>;

<CAMPOS2>	   ::= id#21 <AS> <RCAMPOS> | "*"#23;

<RCAMPOS>           ::= "," <CAMPO> <RCAMPOS> | Ó;

<CAMPO>             ::= id#5"."id#21 <AS>;
<AS>		   ::= AS "`" id#22 "`" | Ó;

<TABELAS>           ::= <TABELA> <RTABELAS>;

<RTABELAS>          ::= "," <TABELAS> | Ó;

<TABELA>            ::= id#24 ;

<CLAUSULA_WHERE>    ::= WHERE <CONDICOES>| Ó;
<CLAUSUALA_ORDER>   ::= ORDER BY <CAMPO><RCAMPOS>#28 <DESC>#29| Ó;
<DESC>		    ::= DESC | Ó;
<CLAUSULA_JOIN>     ::= Ó | INNER JOIN id#25 | LEFT JOIN id#26 | RIGHT JOIN id#27;

<CONDICOES>         ::= <CONDICAO> <RCONDICAO>;

<CONDICAO>          ::= <CAMPO2> <OPERADOR_REL>#18 <CONDICAO2>;
<CONDICAO2>	   ::= <CAMPO2> | <VALOR>#30;
<CAMPO2>             ::= id#5"."id#21;

<RCONDICAO>         ::= <OPERADOR_LOG>#17 <CONDICOES> |  Ó;

<OPERADOR_REL>      ::= "=" | ">" | "<" | ">=" | "<=" | "<>";

<OPERADOR_LOG>      ::= AND | OR;

<EXCLUIR>	    ::= DROP <EXCLUIR2>;

<EXCLUIR2>          ::= TABLE id#13 | INDEX id#14; 

<DESCREVER>         ::= DESCRIBE id#15; 

<SETAR_BANCO>       ::= SET DATABASE id#16;
